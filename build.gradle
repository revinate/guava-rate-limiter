apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group 'com.revinate'
version = '19.1'

if (!hasProperty('sonatypeUsername')) {
    ext.sonatypeUsername = null
    ext.sonatypePassword = null
}

jar {
    baseName = 'guava-rate-limiter'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                artifactId 'guava-rate-limiter'
                packaging 'jar'
                name 'Guava Rate Limiter'
                description 'Rate limiter implementation from the Guava libraries'
                url 'https://github.com/revinate/guava-rate-limiter'

                scm {
                    url 'https://github.com/revinate/guava-rate-limiter'
                    connection 'scm:git:git://github.com/revinate/guava-rate-limiter.git'
                    developerConnection 'scm:git:ssh://git@github.com:revinate/guava-rate-limiter.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'revinate'
                        name 'Revinate, Inc'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
